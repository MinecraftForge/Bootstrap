import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'net.minecraftforge.licenser' version '1.0.1'
    id 'net.minecraftforge.gradleutils' version '[2.3,2.4)'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

gradleutils.gitRoot = rootProject.layout.projectDirectory
group 'net.minecraftforge'
version = gradleutils.tagOffsetVersion

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
    implementation project(':bs-api')
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(16)
    withSourcesJar()
}

license {
    header = rootProject.file("LICENSE-header.txt")
    newLine = false
}

jar {
    manifest.attributes([
        'Specification-Title':   'BootStrap-DEV',
        'Specification-Vendor':  'Forge Development LLC',
        'Specification-Version':  gradleutils.gitInfo.tag,
        'Implementation-Title':   project.name,
        'Implementation-Vendor': 'Forge Development LLC',
        'Implementation-Version': project.version
    ] as java.util.LinkedHashMap, 'net/minecraftforge/bootstrap/dev/')
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'bootstrap-dev'
        pom {
            name = 'Bootstrap-Dev'
            description = 'Dev jar for https://github.com/MinecraftForge/Bootstrap'
            url = 'https://github.com/MinecraftForge/Bootstrap'
            PomUtils.setGitHubDetails(pom, 'Bootstrap')
            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}

// Hack eclipse into knowing that the gradle deps are modules
eclipse.classpath {
    containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
    file.whenMerged { entries.findAll { it.kind == 'lib' || it.path == 'org.eclipse.buildship.core.gradleclasspathcontainer' }.each { it.entryAttributes['module'] = 'true' } }
}
