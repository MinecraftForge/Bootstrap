import net.minecraftforge.gradleutils.PomUtils

plugins {
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'net.minecraftforge.gradleutils' version '[2.1.2,)'
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

gradleutils.gitRoot = rootProject.layout.projectDirectory
group 'net.minecraftforge'
version = gradleutils.tagOffsetVersion

repositories {
    mavenCentral()
    maven gradleutils.forgeMaven
}

dependencies {
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(8)
    withSourcesJar()
}

license {
    header = rootProject.file("LICENSE-header.txt")
    newLine = false
}

jar {
    manifest.attributes([
        'Automatic-Module-Name': 'net.minecraftforge.bootstrap.shim',
        'Main-Class':            'net.minecraftforge.bootstrap.shim.Main'
    ] as java.util.LinkedHashMap)
    manifest.attributes([
        'Specification-Title':   'BootStrap-Shim',
        'Specification-Vendor':  'Forge Development LLC',
        'Specification-Version':  gradleutils.gitInfo.tag,
        'Implementation-Title':   project.name,
        'Implementation-Vendor': 'Forge Development LLC',
        'Implementation-Version': project.version
    ] as java.util.LinkedHashMap, 'net/minecraftforge/bootstrap/shim/')
}

publishing {
    publications.register('mavenJava', MavenPublication) {
        from components.java
        artifactId = 'bootstrap-shim'
        pom {
            name = 'Bootstrap-shim'
            description = 'A executable shim that initalizes Bootstrap. Compatible with java 8 we we can display a nice error.'
            url = 'https://github.com/MinecraftForge/Bootstrap'
            PomUtils.setGitHubDetails(pom, 'Bootstrap')
            license PomUtils.Licenses.LGPLv2_1

            developers {
                developer PomUtils.Developers.LexManos
            }
        }
    }
    repositories {
        maven gradleutils.publishingForgeMaven
    }
}
